apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7
version = '1.0'

group = "net.joningi"
archivesBaseName = "icndb-java-api"

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.glassfish.jersey.core', name: 'jersey-client', version: '2.+'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.+'
    compile group: 'com.google.guava', name: 'guava', version: '16.+'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}


task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Example Application'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Java API client for The Internet Chuck Norris Database (http://www.icndb.com/)'
                url 'https://github.com/joningis/icndb-java-api'

                scm {
                    connection 'scm:git:https://github.com/joningis/icndb-java-api'
                    developerConnection 'scm:git:https://github.com/joningis/icndb-java-api'
                    url 'https://github.com/joningis/icndb-java-api'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'joningi'
                        name 'Jon Ingi Sveinbjornsson'
                        email 'joningis@gmail.com'
                    }
                }
            }
        }
    }
}